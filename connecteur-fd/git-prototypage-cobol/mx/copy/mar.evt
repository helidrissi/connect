      * mar.evt
      * mar.evt is generated from W:\mx\spro\mar.Psf
      * This is a generated file. DO NOT modify this file directly.


       Screen1-Event-Proc.
           .

       Screen1-Gd-1-Event-Proc.
      * 
           EVALUATE Event-Type
           WHEN Msg-Begin-Entry
              PERFORM Screen1-Gd-1-Ev-Msg-Begin-Entry
           WHEN Msg-Bitmap-Clicked
              PERFORM Screen1-Gd-1-Ev-Msg-Bitmap-Clicked
           WHEN Msg-End-Drag
              PERFORM Screen1-Gd-1-Ev-Msg-Goto-Cell
           WHEN Msg-Finish-Entry
              PERFORM Screen1-Gd-1-Ev-Msg-Finish-Entry
           WHEN Msg-Goto-Cell
              PERFORM Screen1-Gd-1-Ev-Msg-Goto-Cell
           WHEN Msg-Goto-Cell-Drag
              PERFORM Screen1-Gd-1-Ev-Msg-Goto-Cell
           WHEN Msg-Goto-Cell-Mouse
              PERFORM Screen1-Gd-1-Ev-Msg-Goto-Cell
           WHEN Msg-Heading-Clicked
              PERFORM Screen1-Gd-1-Ev-Msg-Heading-Clicked
           END-EVALUATE
           .
      ***   start event editor code   ***
           COPY "W:/MISTRAL/COPY/ENVMIS.CBL".

       Init-BMP.

       Open-File-Rtn.
           OPEN I-O MXMARQUE
           PERFORM STAT
           .
       Close-File-Rtn.
           CLOSE MXMARQUE
           .
       Affiche-Grille.
      * Affiche la grille de sélection après l'avoir initialisé.
      * ---------------------------------------------------------------

      * Remise à zéro de la grille...
           MODIFY Screen1-Gd-1, MASS-UPDATE = 1, RESET-GRID = 1
           IF (CREAT-PALM = "X") OR (MODIF-PALM = "X") THEN
               MODIFY Screen1-Gd-1, NUM-ROWS = 0
           ELSE
               MODIFY Screen1-Gd-1, NUM-ROWS = -1
           END-IF
           PERFORM Acu-Screen1-Gd-1-Content
           IF MXASS-PALM = 1
              MODIFY Screen1-Gd-1(1 4), CELL-DATA = "DARVA"
           END-iF
           

      * Remplissage de la grille...
           INITIALIZE ENR-MMAR
           MOVE 0 TO W-EOF
           EVALUATE W-KEY-NUM
               WHEN 1
                   IF W-KEY-ORDER = "A" THEN
                       START MXMARQUE KEY >= CLE1-MMAR
                           INVALID MOVE 1 TO W-EOF
                       END-START
                   ELSE
                       MOVE HIGH-VALUE TO CLE1-MMAR
                       START MXMARQUE KEY <= CLE1-MMAR
                           INVALID MOVE 1 TO W-EOF
                       END-START
                   END-IF
               WHEN 2
                   IF W-KEY-ORDER = "A" THEN
                       START MXMARQUE KEY >= CLE2-MMAR
                           INVALID MOVE 1 TO W-EOF
                       END-START
                   ELSE
                       MOVE HIGH-VALUE TO CLE2-MMAR
                       START MXMARQUE KEY <= CLE2-MMAR
                           INVALID MOVE 1 TO W-EOF
                       END-START
                   END-IF
               WHEN OTHER
                   IF W-KEY-ORDER = "A" THEN
                       START MXMARQUE KEY >= CLE3-MMAR
                           INVALID MOVE 1 TO W-EOF
                       END-START
                   ELSE
                       MOVE HIGH-VALUE TO CLE3-MMAR
                       START MXMARQUE KEY <= CLE3-MMAR
                           INVALID MOVE 1 TO W-EOF
                       END-START
                   END-IF
           END-EVALUATE
           PERFORM STAT
           IF W-EOF = 0 THEN
               IF W-KEY-ORDER = "A" THEN
                   READ MXMARQUE NEXT END MOVE 1 TO W-EOF END-READ
               ELSE
                   READ MXMARQUE PREVIOUS END MOVE 1 TO W-EOF END-READ
               END-IF
               PERFORM STAT
           END-IF
           PERFORM UNTIL W-EOF = 1
               MOVE COD-MMAR TO Screen1-Gd-1-COD
               MOVE LIB-MMAR TO Screen1-Gd-1-LIB
               MOVE ABR-MMAR TO Screen1-Gd-1-ABR
               IF MODU-TVH = 1 OR MXASS-PALM = 1
                  MOVE TVH-MMAR TO Screen1-Gd-1-TVH
               END-IF
               MODIFY Screen1-Gd-1, RECORD-TO-ADD = Screen1-Gd-1-Record
               IF W-KEY-ORDER = "A" THEN
                   READ MXMARQUE NEXT END MOVE 1 TO W-EOF END-READ
               ELSE
                   READ MXMARQUE PREVIOUS END MOVE 1 TO W-EOF END-READ
               END-IF
               PERFORM STAT
           END-PERFORM

      * Positionnement du curseur sur la grille...
           MOVE 1 TO Event-Data-1 Screen1-Gd-1-Cursor-X
                     Screen1-Gd-1-Cursor-X-Old
           MOVE 2 TO Event-Data-2 Screen1-Gd-1-Cursor-Y
                     Screen1-Gd-1-Cursor-Y-Old
           MODIFY Screen1-Gd-1(2),
                  ROW-COLOR = Screen1-Gd-1-Row-Highlight-Color

      * Indique la clé sélectionnée...
           MODIFY Screen1-Gd-1(1, 4), BITMAP = NULL
           MODIFY Screen1-Gd-1(1, 5), BITMAP = NULL
           IF W-KEY-ORDER = "A" THEN
               MODIFY Screen1-Gd-1(1, 1), BITMAP = FLECHEB24-BMP
               , BITMAP-TRAILING = 1
               MODIFY Screen1-Gd-1(1, 2), BITMAP = FLECHEB24-BMP
               , BITMAP-TRAILING = 1
               MODIFY Screen1-Gd-1(1, 3), BITMAP = FLECHEB24-BMP
               , BITMAP-TRAILING = 1
               MODIFY Screen1-Gd-1(1, W-KEY-NUM),
                      BITMAP = FLECHEB24-BMP, BITMAP-TRAILING = 1
           ELSE
               MODIFY Screen1-Gd-1(1, 1),
                      BITMAP = FLECHEB24-BMP, BITMAP-TRAILING = 1
               MODIFY Screen1-Gd-1(1, 2),
                      BITMAP = FLECHEB24-BMP, BITMAP-TRAILING = 1
               MODIFY Screen1-Gd-1(1, 3),
                      BITMAP = FLECHEB24-BMP, BITMAP-TRAILING = 1
               MODIFY Screen1-Gd-1(1, W-KEY-NUM),
                      BITMAP = FLECHEB24-BMP, BITMAP-TRAILING = 1
           END-IF

           MODIFY Screen1-Gd-1, MASS-UPDATE = 0

           MOVE SPACE TO Screen1-Gd-1-Record

           IF ANNUL-PALM = "X" THEN
               MODIFY Screen1-Pb-Delete, VISIBLE = 0
           ELSE
               MODIFY Screen1-Pb-Delete, VISIBLE = 1
           END-IF
           .
       Mode-Interrogation.
      * Bascule vers le mode "Interrogation"
      * ---------------------------------------------------------------

           MOVE "I" TO CDFB

           UNLOCK MXMARQUE

           MOVE 257 TO Screen1-Gd-1-Row-Highlight-Color
           IF MODIF-PALM = "X" THEN
               MODIFY Screen1-Pb-Modify, VISIBLE = 0
           ELSE
               MODIFY Screen1-Pb-Modify, VISIBLE = 1
           END-IF
           .
       Screen1-BeforeCreate.
           PERFORM Controle-Palme
           PERFORM Open-File-Rtn

           OPEN INPUT MXPARAM
           PERFORM STAT
           MOVE 1 TO W-MXPARAM
           READ MXPARAM
           PERFORM STAT
           CLOSE MXPARAM
           .
           

       Screen1-AfterInitData.
160819     MOVE Screen1-Handle TO HANDLE-PALM
      * Gestion de PALM par le programme :
      *  - Le droit de créer est ignoré pour tout utilisateur n'ayant
      *    pas le droit de modifier.
      *  - Le droit d'interrogation est supposé vrai, PALM ayant du
      *    filtrer avant.
      *     IF MODU-TVH = 0 AND MXASS-PALM = 0
      *        MODIFY Screen1-Gd-1, 
      *        DATA-COLUMNS (1, 5, 25, 33, 34), 
      *        DISPLAY-COLUMNS (1, 9, 44, 69, 59).
           
           PERFORM Mode-Interrogation

           PERFORM Affiche-Grille
           .
       Screen1-Gd-1-Ev-Msg-Begin-Entry.
      * Grille verrouillée en mode "Interrogation"...
           IF CDFB = "I" THEN
               MOVE Event-Action-Fail TO Event-Action
               EXIT PARAGRAPH
           END-IF

           INQUIRE Screen1-Gd-1(Event-Data-2)
                   RECORD-DATA IN Screen1-Gd-1-Record
           MOVE Screen1-Gd-1-Record TO Screen1-Gd-1-Record-Old
           MOVE SPACE TO CDF

      * Pas de modification de la 1ère colonne, sauf en création...
           IF ((NOT Screen1-Gd-1-COD = SPACES) AND (Event-Data-1 = 1))
              OR (CDFB = "I") THEN
               MOVE Event-Action-Fail TO Event-Action
               EXIT PARAGRAPH
           END-IF
           IF NOT Screen1-Gd-1-COD = SPACES THEN
               MOVE Screen1-Gd-1-COD TO COD-MMAR
               READ MXMARQUE WITH LOCK INVALID 
                   MOVE "Problème Lecture" TO ACU-MSG-1
                   PERFORM MSG-ERREUR
                   MOVE Event-Action-Fail TO Event-Action
                   EXIT PARAGRAPH
               END-READ
               PERFORM STAT
               IF IND-STAT = 1
                   MOVE "399" TO ACU-MSG-ID
                   PERFORM ACU-SHOW-MSG
                   MOVE Event-Action-Fail TO Event-Action
                   EXIT PARAGRAPH
               END-IF
               
               MOVE "M" TO CDF
           ELSE
               MOVE "C" TO CDF
           END-IF
           .
       Screen1-Gd-1-Ev-Msg-Goto-Cell.
      * La ligne de titre est à ignorer...
           IF Event-Data-2 < 2
               MOVE 2 TO Event-Data-2
           END-IF

      * Alimente la status-bar...
           IF MODU-TVH = 1 OR MXASS-PALM = 1
                MOVE 4 TO NB-COL
           ELSE MOVE 3 TO NB-COL
           END-IF.
           IF Event-Data-1 <= NB-COL THEN
               MODIFY Screen1-St-1-Handle,
                      TITLE = Message-Aide(Event-Data-1)
           ELSE
               PERFORM Mistral-TransferFocus
           END-IF

      * Pour voir si on a changé de ligne
           IF Event-Data-2 = Screen1-Gd-1-Cursor-Y-Old THEN
               MOVE Event-Data-1 TO Screen1-Gd-1-Cursor-X-Old
           ELSE
               MODIFY Screen1-Gd-1(Screen1-Gd-1-Cursor-Y-Old),
                      ROW-COLOR = 0               
               MODIFY Screen1-Gd-1(Event-Data-2),
                      ROW-COLOR = Screen1-Gd-1-Row-Highlight-Color
               IF CDFB = "M" 
                  MODIFY Screen1-Gd-1(Screen1-Gd-1-Cursor-Y-Old, 4),
                         BITMAP = SPACES
                  modify Screen1-Gd-1(Event-Data-2, 4), 
                         BITMAP = ZOOMxxx12-BMP
               END-IF
           END-IF

           MOVE Event-Data-1 TO Screen1-Gd-1-Cursor-X
                                Screen1-Gd-1-Cursor-X-Old
           MOVE Event-Data-2 TO Screen1-Gd-1-Cursor-Y
                                Screen1-Gd-1-Cursor-Y-Old

           UNLOCK MXMARQUE
           .
       Screen1-Gd-1-Ev-Msg-Finish-Entry.
           INQUIRE Screen1-Gd-1(Screen1-Gd-1-Cursor-Y),
                   RECORD-DATA IN Screen1-Gd-1-Record

      * Ne pas considérer un code vide comme valide...
           IF Screen1-Gd-1-COD = SPACE THEN
               MOVE "381" TO Acu-Msg-Id
               PERFORM Acu-Show-Msg
               MOVE Event-Action-Ignore TO Event-Action
               EXIT PARAGRAPH
           END-IF

           IF (Screen1-Gd-1-Cursor-X = 1) AND (CDF = "C") AND
              (NOT Screen1-Gd-1-COD = SPACE) THEN
               MOVE Screen1-Gd-1-COD TO COD-MMAR
               READ MXMARQUE WITH LOCK NOT INVALID
                   MOVE "382" TO Acu-Msg-Id
                   PERFORM Acu-Show-Msg
                   MOVE SPACE TO Screen1-Gd-1-COD
                   MOVE Event-Action-Fail TO Event-Action
                   EXIT PARAGRAPH
               END-READ
           END-IF
           IF NOT Screen1-Gd-1-RECORD = Screen1-Gd-1-Record-Old THEN
               MOVE Screen1-Gd-1-COD TO COD-MMAR
               MOVE Screen1-Gd-1-LIB TO LIB-MMAR
               MOVE Screen1-Gd-1-ABR TO ABR-MMAR

               IF CDF = "C" THEN
                   WRITE ENR-MMAR
                   MOVE COD-MMAR TO SV-MAR-CREE
               ELSE
                   REWRITE ENR-MMAR
               END-IF
               PERFORM STAT
               MOVE SPACE TO CDF
           END-IF
           .
       Screen1-Gd-1-Ev-Msg-Heading-Clicked.
           IF Event-Data-1 <= 3 THEN
               IF Event-Data-1 = W-KEY-NUM THEN
                   IF W-KEY-ORDER = "A" THEN
                       MOVE "D" TO W-KEY-ORDER
                   ELSE
                       MOVE "A" TO W-KEY-ORDER
                   END-IF
               ELSE
                   MOVE Event-Data-1 TO W-KEY-NUM
               END-IF
               PERFORM Mode-Interrogation
               PERFORM Affiche-Grille
           END-IF
           .
       Screen1-Pb-Quit-LinkTo.
           PERFORM Acu-Screen1-EXIT
           .
       Screen1-Ev-Cmd-Close.
           PERFORM Mistral-WindowClosing
           .
       Screen1-Pb-Refresh-LinkTo.
           PERFORM Mode-Interrogation

           PERFORM Affiche-Grille
           .
       Screen1-Pb-Modify-LinkTo.
      * PALM interdit la mise à jour...
           IF MODIF-PALM = "X" THEN
               MOVE "380" TO Acu-Msg-Id
               PERFORM Acu-Show-Msg
               Move Event-Action-Ignore TO Event-Action
               EXIT PARAGRAPH
           END-IF

           MOVE "M" TO CDFB

           MOVE 482 TO Screen1-Gd-1-Row-Highlight-Color
           MODIFY Screen1-Gd-1(Screen1-Gd-1-Cursor-Y),
                  ROW-COLOR = Screen1-Gd-1-Row-Highlight-Color
           IF MODU-TVH = 1 OR MXASS-PALM = 1
              modify Screen1-Gd-1(Screen1-Gd-1-Cursor-Y, 4), 
                     BITMAP = ZOOMxxx12-BMP
           END-IF
           MODIFY Screen1-Pb-Modify, VISIBLE = 0
           
           MOVE 1 TO Screen1-Gd-1-Cursor-X
           MODIFY Screen1-Gd-1, CURSOR-X = Screen1-Gd-1-Cursor-X

      * ATTENTION : Utilisable uniquement dans un traitement déclenché
      *             par une EXCEPTION
           ACCEPT Screen1-Gd-1
           .
       Screen1-Pb-Delete-LinkTo.
      * PALM interdit la suppression.
           IF ANNUL-PALM = "X" THEN
               MOVE "380" TO Acu-Msg-Id
               PERFORM Acu-Show-Msg
               MOVE Event-Action-Ignore TO Event-Action
               EXIT PARAGRAPH
           END-IF

           MOVE "203" TO Acu-Msg-Id
           PERFORM Acu-Show-Msg
           IF NOT Acu-Return-Value = 1 THEN
               EXIT PARAGRAPH
           END-IF
           INQUIRE Screen1-Gd-1, CURSOR-X IN Screen1-Gd-1-Cursor-X,
                                 CURSOR-Y IN Screen1-Gd-1-Cursor-Y
           INQUIRE Screen1-Gd-1(Screen1-Gd-1-Cursor-Y),
                   RECORD-DATA IN Screen1-Gd-1-Record
           MOVE Screen1-Gd-1-COD TO COD-MMAR
           READ MXMARQUE WITH LOCK
           PERFORM STAT
           DELETE MXMARQUE
           PERFORM STAT
           MODIFY Screen1-Gd-1,
                      RECORD-TO-DELETE = Screen1-Gd-1-Cursor-Y-Old,
                      ROW-COLOR = Screen1-Gd-1-Row-Highlight-Color
           
           .

       Screen1-Aft-Routine.
           PERFORM Close-File-Rtn
           .
      *
       Screen1-Pb-Print-Ev-Cmd-Clicked.
                         
           .


      * Liste des codes TVH
       Screen1-Gd-1-Ev-Msg-Bitmap-Clicked.
           .

       
